module complex

-- A simple complex number class with static typing. This example illustrates
-- the following features:
--
--   * class definition
--   * module definition
--   * operator overloading
--   * intersection types
--   * casts
--   * interface implementation

import math


class Complex implements Addable<Float, Complex>,
                         Multipliable<Float, Complex>
  const re as Float
  const im as Float

  def create(re as Float, im as Float)
    self.re = Float(re)
    self.im = Float(im)
  end

  def abs() as Float
    return Sqrt(self.re**2 + self.im**2)
  end

  def conj() as Complex
    return Complex(self.re, -self.im)
  end

  def _str() as Str
    if self.im >= 0
      return "({} + {}j)".format(self.re, self.im)
    else
      return "({} - {}j)".format(self.re, -self.im)
    end
  end

  def _eq(n as Object) as Boolean
    if n is Complex
      var c = (n as Complex)
      return self.re == c.re and self.im == c.im
    elif n is Float
      return self.re == (n as Float) and self.im == 0.0
    else
      return False
    end
  end

  def _add(n as Complex) as Complex or
          (n as Float) as Complex
    var c = Coerce(n)
    return Complex(self.re + c.re, self.im + c.im)
  end

  def _sub(n as Complex) as Complex or
          (n as Float) as Complex
    var c = Coerce(n)
    return Complex(self.re - c.re, self.im - c.im)
  end

  def _mul(n as Complex) as Complex or
          (n as Float) as Complex
    var c = Coerce(n)
    return Complex(self.re * c.re - self.im * c.im,
                   self.im * c.re + self.re * c.im)
  end

  def _div(n as Complex) as Complex or
          (n as Float) as Complex
    var c = Coerce(n)
    var d = c.re**2 + c.im**2
    return Complex((self.re * c.re + self.im * c.im) / d,
                   (self.im * c.re - self.re * c.im) / d)
  end
end


private def Coerce(n as Complex) as Complex or
                  (n as Float) as Complex
  if n is Complex
    return n as Complex
  else
    return Complex(n as Float, 0.0)
  end
end
