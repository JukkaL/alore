-- Usage: wordfreq.alo N FILE ...
--
-- Display the N most frequent words in file(s). Assume that the files are
-- text files encoded using the platform default encoding.
--
-- This example uses static typing.

import io, re


def Main(args as Array<Str>)
  if args.length() < 2
    WriteLn('Usage: wordfreq.alo N FILE ...')
    Exit(1)
  end

  -- Construct a map (hash table) from Str to Int.
  var map = Map() as <Str, Int>
  var n = Int(args[0])

  -- Calculate word counts.
  for fnam in args[1:]
    var s = TextFile(fnam).read()
    for word in Subst(s, '\W', ' ').split()
      map[word] = map.get(word, 0) + 1
    end
  end

  -- Construct an array of (int, string) tuples.
  var a = [] as <(Int, Str)>
  for word, freq in map
    a.append((freq, word))
  end

  -- Display the n most frequent words with frequencies.
  for freq, word in Reversed(Sort(a)[-n:])
    WriteLn('{-6:} {}'.format(freq, word))
  end
end
