@head
@title Tuple types

<p>Tuple objects are common in Alore programs. The type system has special
support for representing tuples in a type-safe manner.

@h2 Tuple basics

<p>For example, consider a simple 2-tuple type <tt>(Int, Str)</tt>. The first
item is an integer and the second item a string. The program below defines 
<tt>t</tt> with type <tt>(Int, Str)</tt> and assigns a tuple value to it:

@example
  var t as (Int, Str)
  t = 2, 'foo'
@end

<p>When you access tuple items using an integer literal, the type checker
infers the type precisely:

@example
  t[0]       -- Type Int
  t[1]       -- Type Str
@end

@h2 Constructing tuples with explicit types

<p>The example program above relied on type inference when constructing the
tuple. You can also give the types explicitly using the now-familiar syntax
(note that the parentheses are necessary due to precedence):

@example
  t = (2, 'foo') as &lt;Int, Str&gt;
@end

@h2 General tuple types

<p>More generally, a tuple type <tt>(T1, T2, ...)</tt> represents a tuple that
has T1 as the first item type, T2 as the second item type, etc. Empty tuples
and 1-tuples are special:

<ul>
  <li><tt>()</tt> is the empty tuple type.
  <li><tt>(T,)</tt> is a 1-tuple (note the extra comma).
</ul>

@h2 Tuple types and multiple assignment

<p>You can use tuple types for multiple assignment:

@example
  var t as (Int, Str)
  var i as Int
  var s as Str
  t = (1, 'foo')     -- Parentheses are optional, but may improve clarity
  i, s = t
@end

@h2 Tuple types and variable definition

<p>If you define multiple variables using <tt>var</tt> and cannot use type
inference, you can define the variable types using a tuple type:

@example
  var i, s as (Int, Str)     -- i has type Int, s has type Str
  i = 1
  s = 'foo'
@end

<p>The same syntax also supports an initializer:

@example
  var i, s = (1, 'foo') as (Int, Str)
@end

<p>Note that in the above example the <tt>as (...)</tt> construct is bound to
the variable definition, not to the tuple expression.

@h2 The <tt>Tuple</tt> type

<p>The runtime type of all tuples is <tt>Tuple</tt>, independent of the number
of items and their types. <tt>Tuple</tt> also acts as the supertype of all
tuple types.
Values of type <tt>Tuple</tt> support all tuple operations, but they do not
include precise tuple item type information.

<p>All tuple items have type <tt>Object</tt> when accessed using
a <tt>Tuple</tt> value:

@example
  var t as Tuple
  t = (1, 'x')     -- Ok
  t[0]             -- Type Object, not Int, since accessing using type Tuple
@end
